Асинх Функция ПолучитьИмяФайлаАсинх(Режим) Экспорт

	Проводник = Новый ДиалогВыбораФайла(Режим);
	
	Если Режим = РежимДиалогаВыбораФайла.Открытие Тогда
		Проводник.ПроверятьСуществованиеФайла = Истина;
	КонецЕсли;
	
	Результат = ЖДАТЬ Проводник.ВыбратьАсинх();
	
	Если Результат = Неопределено Тогда
	    ВызватьИсключение "Не удалось получить имя файла";
	КонецЕсли;
	
	Возврат Проводник.ПолноеИмяФайла;
	
КонецФункции // ПолучитьИмяФайлаАсинх()

Асинх Функция ПолучитьОписаниеФайлаАсинх(ИмяФайла) Экспорт
	
	
	Файл = Новый Файл(ИмяФайла);
	
	Если Не (Ждать Файл.СуществуетАсинх()) Тогда
		ВызватьИсключение "Файла не существует: " + ИмяФайла;
	КонецЕсли;
	
	ОписаниеФайла = Новый Структура("РазмерФайла, РасширениеФайла");
	ОписаниеФайла.РазмерФайла = Ждать Файл.РазмерАсинх();
	ОписаниеФайла.РасширениеФайла = Файл.Расширение;
	
	Возврат ОписаниеФайла;
	  	
	  
КонецФункции // ПолучитьОписаниеФайлаАсинх()

Асинх Функция ПолучитьАдресИОписаниеФайлаАсинх(УникальныйИдентификатор, ЭтоКартинка = Ложь) Экспорт
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ИмяФайла = Ждать ПолучитьИмяФайлаАсинх(Режим);
	
	ОписаниеФайла = Ждать ПолучитьОписаниеФайлаАсинх(ИмяФайла);
	
	РасширениеКартинок = ".jpg .jpeg .png .gif .svg";
	
	Если СтрНайти(РасширениеКартинок, ОписаниеФайла.РасширениеФайла) = 0 И ЭтоКартинка Тогда
		ВызватьИсключение "Выбранный файл не является картинкой!";
	КонецЕсли;
	
	ПомещаемыйФайл = Ждать ПоместитьФайлНаСерверАсинх(, , , ИмяФайла, УникальныйИдентификатор);
	
	АдресФайла = ПомещаемыйФайл.Адрес;
	
	ОписаниеФайла.Вставить("Адрес", АдресФайла);
	
	Возврат ОписаниеФайла; 
	
	

КонецФункции // ПолучитьАдресИОписаниеФайлаАсинх()

